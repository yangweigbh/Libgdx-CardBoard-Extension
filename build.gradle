ext {
     gdxVersion='1.9.4'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

project(":libgdx-cardboard-core") {
	apply plugin: "java"
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'signing'
	apply plugin: 'maven'

	group = 'com.github.yangweigbh'
	version = '0.3'

	dependencies {
	    compile "com.badlogicgames.gdx:gdx-backend-android:${gdxVersion}"
	    compile "com.badlogicgames.gdx:gdx:${gdxVersion}"
	    compile fileTree(dir: 'libs', include: ['*.jar'])
	}

	sourceSets {
	    main {
	        java {
	            srcDir 'src'
	        }
	        resources {
	            srcDir 'src'
	        }
	    }
	}

	signing {
	    sign configurations.archives
	}

	task generatePom << {
	    pom {
	        project {
	            name 'libgdx-cardboard-core'
		        packaging 'jar'
		        // optionally artifactId can be defined here 
		        description 'library help developing cardboard app based on libgdx'
		        url 'https://github.com/yangweigbh/Libgdx-CardBoard-Extension'

		        scm {
		          connection 'scm:git:https://github.com/yangweigbh/Libgdx-CardBoard-Extension.git'
		          developerConnection 'scm:git:https://github.com/yangweigbh'
		          url 'https://github.com/yangweigbh/Libgdx-CardBoard-Extension.git'
		        }

		        licenses {
		          license {
		            name 'The Apache License, Version 2.0'
		            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
		          }
		        }

		        developers {
		          developer {
		            name 'Wei Yang'
		            email 'yangweigbh@hotmail.com'
		          }
		        }
	        }
	    }.writeTo("$buildDir/libs/${project.name}-${version}.pom")
	}

	task sourcesJar(type: Jar, dependsOn: [classes, "generatePom"]) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}

	artifacts {
	    archives sourcesJar
	    archives javadocJar
	    archives file("$buildDir/libs/${project.name}-${version}.pom")
	}

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	bintray {
	    user = System.getenv('BINTRAY_USER')
	    key = System.getenv('BINTRAY_KEY')
	    configurations = ['archives']
		pkg {
			repo = 'maven'
			name = 'libgdx-cardboard-core'
			licenses = ['Apache-2.0']
			vcsUrl = 'https://github.com/yangweigbh/Libgdx-CardBoard-Extension.git'
			version {
				name = '0.3'
				desc = 'libgdx cardboard extension 0.3'
				vcsTag = '0.3.0'
			}
		}
	}

	uploadArchives {
	  repositories {
	    mavenDeployer {
	      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

	      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	        authentication(userName: ossrhUsername, password: ossrhPassword)
	      }

	      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	        authentication(userName: ossrhUsername, password: ossrhPassword)
	      }

	      pom.project {
	            name 'libgdx-cardboard-core'
		        packaging 'jar'
		        // optionally artifactId can be defined here 
		        description 'library help developing cardboard app based on libgdx'
		        url 'https://github.com/yangweigbh/Libgdx-CardBoard-Extension'

		        scm {
		          connection 'scm:git:https://github.com/yangweigbh/Libgdx-CardBoard-Extension.git'
		          developerConnection 'scm:git:https://github.com/yangweigbh'
		          url 'https://github.com/yangweigbh/Libgdx-CardBoard-Extension.git'
		        }

		        licenses {
		          license {
		            name 'The Apache License, Version 2.0'
		            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
		          }
		        }

		        developers {
		          developer {
		            name 'Wei Yang'
		            email 'yangweigbh@hotmail.com'
		          }
		        }
		    }
	    }
	  }
	}
}

project(":Demo") {
	apply plugin: 'com.android.application'

	android {
	    compileSdkVersion 23
	    buildToolsVersion "23.0.1"

	    defaultConfig {
	        minSdkVersion 9
	        targetSdkVersion 23
	        versionCode 1
	        versionName "1.0"
	    }

	    sourceSets {
	        main {
	            manifest.srcFile 'AndroidManifest.xml'
	            java.srcDirs = ['src']
	            resources.srcDirs = ['src']
	            aidl.srcDirs = ['src']
	            renderscript.srcDirs = ['src']
	            res.srcDirs = ['res']
	            assets.srcDirs = ['assets']
	        }

	        androidTest.setRoot('tests')
	    }
	}

	dependencies {
	    compile "com.badlogicgames.gdx:gdx-backend-android:${gdxVersion}"
	    compile "com.badlogicgames.gdx:gdx:${gdxVersion}"
	    compile fileTree(dir: 'libs', include: ['*.jar'])
	    compile "com.github.yangweigbh:libgdx-cardboard-core:0.3"
	}

	tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
        pkgTask.jniFolders = new HashSet<File>()
        pkgTask.jniFolders.add(new File(projectDir, 'libs'))
    }
}